/* --------------------------------------------------
3:3 기준 
현재 P1~3/ P4~6 이 각각 팀

P1, P4는 User Select 으로)
나머지는 아무종족

기존 (밀리) 트리거를 삭제할 필요가 있음
로케이션을 하나 생성 해야함 (선택인식)

랜덤위치 트리거는 없음,
만들 의향은 있지만 만들게 되면 사용방법이 더 복잡해짐

v.152
단순 파일 분리
sheun_t_@naver.com
--------------------------------------------------*/
import shareTeam;
import shareResource;
import shareSupply;
import shareUpgrade;
import shareUnit;
import shareTech;

function meleeTrigger(teamID)
{
	const headPlayer = teamID*shareTeam.TeamManNumber;
	//start 50 ore
	if(Deaths(headPlayer, Exactly, 0, 215))
	{
		SetResources(headPlayer, SetTo, 50, Ore);
		SetDeaths(headPlayer, SetTo, 1, 215);
	}
	// defeat, victory
	if(Command(headPlayer, Exactly, 0, '(buildings)'))
	{
		Defeat();
	}

}

function ShowText()
{
	EUDPlayerLoop()();
	SetMissionObjectives('\x0610li\x01: 자원, 인구, 업글, 테크 공유 적용됨');
	EUDEndPlayerLoop();
}

// main.eps -----------------------------------------------------------
const UnitID_DeathCounter1	= 205; //leftdoor
const UnitID_DeathCounter2	= 206; //door

function DeathCount()
{//timer
	if(Deaths($P8, AtLeast, 1, UnitID_DeathCounter1))
		SetDeaths($P8, Subtract, 1, UnitID_DeathCounter1);

	if(Deaths($P8, AtLeast, 1, UnitID_DeathCounter2))
		SetDeaths($P8, Subtract, 1, UnitID_DeathCounter2);

}

function onPluginStart()
{
	ShowText();
	
	// define 팀 i을 x종족으로
	for(var i=0; i<shareTeam.TeamNumber; i++)
	{
		const teamHead = i*shareTeam.TeamManNumber;
		if(Command(teamHead, Exactly, 1, $U('Zerg Hatchery'))){
			shareTeam.SetTeam(i, 1);
		}
		if(Command(teamHead, Exactly, 1, $U('Terran Command Center'))){
			shareTeam.SetTeam(i, 2);
		}
		if(Command(teamHead, Exactly, 1, $U('Protoss Nexus'))){
			shareTeam.SetTeam(i, 3);
		}
	}
}

function beforeTriggerExec() 
{
	DeathCount();
	for(var iTeam=0; iTeam<shareTeam.TeamNumber; iTeam++)
	{//team 0~1
		// 밀리 트리거
		meleeTrigger(iTeam);
		if(Deaths($P8, Exactly, 0, UnitID_DeathCounter1))
		{// 1/24 sec
			for(var p=0; p<6; p++)
			{//p1~6
				if(playerexist(p))
				{
					shareResource.IsDifferResource(p, iTeam);	// 팀간 자원공유
					shareSupply.IsDifferSupplies(p, iTeam);	// 팀간 인구공유 
					shareUnit.GiveSelectedUnit(p);		// 팀간 선택한 유닛주기
				}
			}
			SetDeaths($P8, SetTo, 2, UnitID_DeathCounter1);
		}

		if(Deaths($P8, Exactly, 0, UnitID_DeathCounter2))
		{// 1 sec
			shareTech.SetSharedTech(iTeam);			// 팀간 기술공유
			shareUpgrade.SetSharedUpgrade(iTeam);		// 팀간 업글공유
			SetDeaths($P8, SetTo, 48, UnitID_DeathCounter2);
		}
	}
}

function afterTriggerExec() 
{
	// eudturbo
	SetMemory(0x6509A0, SetTo, 0);  
}
